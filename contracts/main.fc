#include "imports/stdlib.fc";
#include "imports/op-codes.fc";
#include "imports/jetton-utils.fc";
#include "imports/utils.fc";

(slice, slice, cell, slice, cell, cell) load_data() inline {
    ;; in storage:
    ;; - usdt jetton master address
    ;; - root jetton master address
    ;; - user sc code
    ;; - admin address
    ;; - usdt jetton wallet code
    ;; - beetroot jetton wallet code
    slice ds = get_data().begin_parse();
    return (ds~load_msg_addr(), ds~load_msg_addr(), ds~load_ref(), ds~load_msg_addr(), ds~load_ref(), ds~load_ref());
}

() send_tokens(int balance, slice owner_address, int query_id, slice jetton_wallet_address) impure inline {
    cell msg_body = begin_cell()
                    .store_uint(op::transfer, 32)
                    .store_uint(query_id, 64)
                    .store_coins(balance)
                    .store_slice(owner_address)
                    .store_slice(my_address())
                    .store_uint(0, 1) 
                    .store_coins(1000000) ;; 0.001 tons
                    .store_uint(0, 1) 
                    .end_cell();
    cell msg = begin_cell()
                .store_uint(0x18, 6)
                .store_slice(jetton_wallet_address)
                .store_coins(30000000) ;; 0.03 tons
                .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1) 
                .store_ref(msg_body)
               .end_cell();
    send_raw_message(msg, SEND_MODE_REGULAR);
}

() mint_tokens(int query_id, int balance, slice owner_address, slice root_master_address) impure inline {
    ;; default tokens mint msg ref: https://github.com/ton-blockchain/token-contract/blob/21e7844fa6dbed34e0f4c70eb5f0824409640a30/ft/jetton-minter.fc#L59    
    cell internal_transfer_body = begin_cell()
                                .store_uint(op::internal_transfer, 32)
                                .store_uint(query_id, 64)
                                .store_coins(balance)
                                .store_slice(my_address())
                                .store_slice(owner_address)
                                .store_coins(1000000) ;; 0.001 tons
                                .store_uint(0, 1)
                                .end_cell();
    slice mint_token_body = begin_cell()
                            .store_uint(op::mint_token, 32)
                            .store_uint(query_id, 64)
                            .store_slice(owner_address)
                            .store_coins(20000000) ;; 0.02 tons
                            .store_ref(internal_transfer_body)
                            .end_cell()
                            .begin_parse();
    cell mint_token_msg = begin_cell()
                        .store_uint(0x18, 6) 
                        .store_slice(root_master_address)
                        .store_coins(30000000) ;; 0.03 tons
                        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1) 
                        .store_slice(mint_token_body)
                        .end_cell();
    send_raw_message(mint_token_msg, SEND_MODE_REGULAR);
}

() mint_user_sc(int query_id, int balance, cell user_sc_code, slice owner_address, slice root_master_address, cell jetton_wallet_code) impure inline {
    cell state_init = calculate_user_sc_state_init(user_sc_code, owner_address, my_address(), root_master_address, jetton_wallet_code);
    slice to_address = calculate_sc_address(state_init);
    cell msg_body = begin_cell()
                    .store_uint(op::deposit, 32)
                    .store_uint(query_id, 64)
                    .store_uint(balance, 64)
                    .store_uint(now(), 32) ;; deposit_timestamp
                    .end_cell();
    cell mint_user_sc_msg = begin_cell()
                            .store_uint(0x10, 6)
                            .store_slice(to_address) 
                            .store_coins(30000000) ;; 0.03 tons
                            .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
                            .store_ref(state_init)
                            .store_ref(msg_body)
                            .end_cell();
    send_raw_message(mint_user_sc_msg, SEND_MODE_REGULAR);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    ;; ignore bounced messages
    if (flags & 1) {
        return ();
    }
    slice sender_address = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);
    (slice usdt_jetton_master_address, slice root_master_address, cell user_sc_code, slice admin_address, cell usdt_jetton_wallet_code, cell jetton_wallet_code) = load_data();
    if (op == op::transfer_notification) {   
        cell state_init = calculate_jetton_wallet_state_init(my_address(), usdt_jetton_master_address, usdt_jetton_wallet_code);
        slice jetton_wallet_address = calculate_jetton_wallet_address(state_init);
        throw_unless(err::not_owner, equal_slices(sender_address, jetton_wallet_address));
        int balance = in_msg_body~load_coins();
        slice owner_address = in_msg_body~load_msg_addr();
        ;; first step
        mint_user_sc(query_id, balance, user_sc_code, owner_address, root_master_address, jetton_wallet_code);
        ;; second step
        mint_tokens(query_id, balance * 10, owner_address, root_master_address);
        ;; third step
        send_tokens(1000000, admin_address, query_id, jetton_wallet_address); ;; 1 usdt
        return ();
    }
    if (op == op::claim) {
        slice admin_address = in_msg_body~load_msg_addr();
        int balance = in_msg_body~load_uint(64);
        int profit = in_msg_body~load_uint(64);
        in_msg_body.end_parse();
        cell state_init = calculate_user_sc_state_init(user_sc_code, admin_address, my_address(), root_master_address, jetton_wallet_code);
        slice user_sc_address = calculate_sc_address(state_init);
        throw_unless(err::not_child, equal_slices(sender_address, user_sc_address));
        ;; sending profit for user
        cell state_init = calculate_jetton_wallet_state_init(my_address(), usdt_jetton_master_address, usdt_jetton_wallet_code);
        slice usdt_jetton_wallet_address = calculate_jetton_wallet_address(state_init);
        send_tokens(balance + profit, admin_address, query_id, usdt_jetton_wallet_address);
        return();
    }
    ;; if wrong op code
    throw(err::unknown_op_code);
}

(slice, slice, cell, slice, cell, cell) get_main_data() method_id {
    return load_data();
}

(slice) get_user_sc_address(slice admin_address) method_id {
    (_, slice root_master_address, cell user_sc_code, _, _, cell jetton_wallet_code) = load_data();
    cell state_init = calculate_user_sc_state_init(user_sc_code, admin_address, my_address(), root_master_address, jetton_wallet_code);
    return calculate_sc_address(state_init);
}