;; ----------------------------------------------------------------------------------------
;; User contract for storing metadata about user deposits and for further yield calculation
;; ----------------------------------------------------------------------------------------

#include "imports/stdlib.fc";
#include "imports/op-codes.fc";
#include "imports/utils.fc";
#include "imports/jetton-wallet-utils.fc";

global slice admin_address;
global slice main_sc_address;
global int usdt_slp_amount;
global int usdt_tlp_amount;
global int total_deposit_amount;
global int root_amount;

() load_data() impure inline_ref {
    slice ds = get_data().begin_parse();

    admin_address = ds~load_msg_addr(); 
    main_sc_address = ds~load_msg_addr();
    usdt_slp_amount = ds~load_coins();
    usdt_tlp_amount = ds~load_coins();
    total_deposit_amount = ds~load_coins();
    root_amount = ds~load_coins();

    ds.end_parse();
}

() save_data() impure inline_ref {
    set_data(
        begin_cell()
        .store_slice(admin_address)
        .store_slice(main_sc_address)
        .store_coins(usdt_slp_amount)
        .store_coins(usdt_tlp_amount)
        .store_coins(total_deposit_amount)
        .store_coins(root_amount)
        .end_cell()
    );
}

() withdraw_notification(int query_id, int final_usdt_slp_amount, int final_usdt_tlp_amount) impure inline {
    cell msg_body = begin_cell()
        .store_uint(op::withdraw_notification, 32)
        .store_uint(query_id, 64)
        .store_slice(main_sc_address)
        .store_coins(final_usdt_slp_amount)
        .store_coins(final_usdt_tlp_amount)
        .end_cell();
    cell msg = begin_cell()
        .store_uint(0x10, 6)
        .store_slice(main_sc_address)
        .store_coins(500000000) ;; 0.5 tons
        .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_ref(msg_body)
        .end_cell();
    send_raw_message(msg, SEND_MODE_REGULAR);
}

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) {
        ;; ignore bounced messages
        return ();
    }
    if (in_msg_body.slice_bits() < 32) {
        ;; ignore simple transfers
        return ();
    }
    slice sender_address = cs~load_msg_addr();
    (int op, int query_id) = (in_msg_body~load_uint(32), in_msg_body~load_uint(64));
    load_data();
    if (op == op::deposit) {
        throw_unless(err::not_parent, equal_slices(sender_address, main_sc_address));

        int new_total_deposit_amount = in_msg_body~load_coins();
        int new_usdt_slp_amount = in_msg_body~load_coins();
        int new_usdt_tlp_amount = in_msg_body~load_coins();
        int new_root_amount = in_msg_body~load_coins();
        in_msg_body.end_parse();

        usdt_slp_amount += new_usdt_slp_amount;
        usdt_tlp_amount += new_usdt_tlp_amount;
        total_deposit_amount += new_total_deposit_amount;
        root_amount += new_root_amount;

        save_data();
        return();
    }
    if (op == op::withdraw_internal) {
        throw_unless(err::not_parent, equal_slices(sender_address, main_sc_address));

        int jetton_amount = in_msg_body~load_coins();
        in_msg_body.end_parse();

        int deposit_share_percentage = jetton_amount * 100 / root_amount;
        int final_usdt_slp_amount = usdt_slp_amount * deposit_share_percentage / 100;
        int final_usdt_tlp_amount = usdt_tlp_amount * deposit_share_percentage / 100;

        withdraw_notification(query_id ,final_usdt_slp_amount, final_usdt_tlp_amount);

        usdt_slp_amount -= final_usdt_slp_amount;
        usdt_tlp_amount -= final_usdt_tlp_amount;
        root_amount -= jetton_amount;
        save_data();
        return();
    }
    throw(err::unknown_op_code);
}

(slice, slice, int, int, int, int) get_user_data() method_id {
    load_data();
    return (
        admin_address,
        main_sc_address,
        usdt_slp_amount,
        usdt_tlp_amount,
        total_deposit_amount,
        root_amount
    );
}